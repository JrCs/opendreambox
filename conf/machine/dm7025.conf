#@TYPE: Machine
#@NAME: Dreambox DM 7025
#@DESCRIPTION: Machine configuration for the Dreambox DM 7025

MACHINE_ESSENTIAL_EXTRA_RDEPENDS = "\
	dreambox-boottool \
	kernel-module-autofs4 \
	kernel-module-cdrom \
	kernel-module-cifs \
	kernel-module-exportfs \
	kernel-module-fat \
	kernel-module-ide-cd \
	kernel-module-loop \
	kernel-module-msdos \
	kernel-module-reiserfs \
	kernel-module-sd-mod \
	kernel-module-sg \
	kernel-module-sqlzma \
	kernel-module-squashfs \
	kernel-module-sr-mod \
	kernel-module-stv0299 \
	kernel-module-unlzma \
	kernel-module-vfat \
	kernel-module-xfs \
	unionfs-modules"

PREFERRED_VERSION_glibc = "2.3.5+cvs20051107"
PREFERRED_VERSION_gcc-cross-initial = "3.4.5"
PREFERRED_VERSION_gcc-cross = "4.1.1"
PREFERRED_VERSION_gcc-cross-sdk = "4.1.1"
PREFERRED_VERSION_gcc = "4.1.1"
PREFERRED_VERSION_gdb = "6.6.50.20061209"
PREFERRED_VERSION_gdb-cross = "6.6.50.20061209"
PREFERRED_VERSION_binutils-cross = "2.16.1"
PREFERRED_VERSION_binutils-cross-sdk = "2.16.1"
PREFERRED_VERSION_binutils = "2.16.1"
PREFERRED_VERSION_squashfs-tools-native = "3.1r2"

KERNEL_VERSION = 2.6.12.6

EXTRA_IMAGECMD_jffs2 = " --eraseblock=0x4000 -n -l "
IMAGE_CMD_jffs2 = "\
	install -d ${DEPLOY_DIR_IMAGE}/boot_tmp; \
	for i in bin dev mnt/flash mnt/squashfs mnt/root; \
	do \
		install -d ${IMAGE_ROOTFS}/boot/$i; \
	done; \
	install -d ${IMAGE_ROOTFS}/boot/lib/modules/${KERNEL_VERSION}/kernel/fs; \
	install -d ${IMAGE_ROOTFS}/boot/lib/modules/${KERNEL_VERSION}/kernel/drivers/block; \
	mv ${IMAGE_ROOTFS}/lib/modules/${KERNEL_VERSION}/kernel/fs/squashfs \
		${IMAGE_ROOTFS}/boot/lib/modules/${KERNEL_VERSION}/kernel/fs; \
	mv ${IMAGE_ROOTFS}/lib/modules/${KERNEL_VERSION}/kernel/fs/unionfs.ko \
		${IMAGE_ROOTFS}/boot/lib/modules/${KERNEL_VERSION}/kernel/fs; \
	mv ${IMAGE_ROOTFS}/lib/modules/${KERNEL_VERSION}/kernel/drivers/block/loop.ko \
		${IMAGE_ROOTFS}/boot/lib/modules/${KERNEL_VERSION}/kernel/drivers/block; \
	mkfs.jffs2 --root=${IMAGE_ROOTFS}/boot --faketime \
		--output=${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.boot.jffs2 \
		${EXTRA_IMAGECMD}; \
	mv ${IMAGE_ROOTFS}/boot/* ${DEPLOY_DIR_IMAGE}/boot_tmp; \
	\
	install -d ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/delta; \
	cd ${IMAGE_ROOTFS}; \
	for i in `find -maxdepth 3 -name enigma2 | xargs`; \
	do \
		install -d ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/delta/${i%%/enigma2}; \
		mv $i ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/delta/${i%%/enigma2}; \
	done; \
	for i in `find -type d -name extra | xargs`; \
	do \
		install -d ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/delta/${i%%/extra}; \
		mv $i ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/delta/${i%%/extra}; \
	done; \
	cat ${IMAGE_ROOTFS}/etc/fstab | sed 's/^\/dev\/mtdblock\/2/\/dev\/root/;' > ${IMAGE_ROOTFS}/etc/fstab_neu; \
	mv -f ${IMAGE_ROOTFS}/etc/fstab_neu ${IMAGE_ROOTFS}/etc/fstab; \
	mksquashfs ${IMAGE_ROOTFS}/* ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/squashfs \
		-root-owned -le -noappend; \
	\
	mkfs.jffs2 --root=${DEPLOY_DIR_IMAGE}/${IMAGE_NAME} --faketime \
		--output=${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.jffs2 \
		${EXTRA_IMAGECMD}; \
	\
	for i in ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/delta/*; \
		do cp -R $i ${IMAGE_ROOTFS}; done; \
	rm -rf ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}; \
	rm -rf ${IMAGE_ROOTFS}/boot; \
	mv ${DEPLOY_DIR_IMAGE}/boot_tmp ${IMAGE_ROOTFS}/boot; \
	buildimage ${STAGING_LIBDIR}/dreambox-secondstage/main.bin.gz \
		${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.boot.jffs2 \
		${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.jffs2 \
		${MACHINE} \
		> ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.nfi"

require conf/machine/include/dreambox-mipsel.inc
